/*
This is the GRADLE build file for LIRE.
 */
apply plugin: 'java'
apply plugin: 'project-report'


sourceCompatibility = 11
version = '1.0_b06'
jar {
    manifest {
        attributes 'Implementation-Title': 'LIRE',
                'Implementation-Version': archiveVersion
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // ---< Lucene >---
    implementation 'org.apache.lucene:lucene-core:8.9.0'
    implementation 'org.apache.lucene:lucene-analyzers-common:8.9.0'
    implementation 'org.apache.lucene:lucene-queryparser:8.9.0'
    implementation 'org.apache.lucene:lucene-queries:8.9.0'
    implementation 'org.apache.lucene:lucene-backward-codecs:8.9.0'

    // ---< Commons >---
    implementation 'commons-io:commons-io:2.6'
    implementation 'org.apache.commons:commons-math3:3.6.1'
    implementation 'commons-codec:commons-codec:1.11'
    implementation 'com.sangupta:jopensurf:1.0.0'

    implementation fileTree(dir: 'lib', include: '*.jar')

    // ---< Helper files, optional >---
    implementation 'com.twelvemonkeys.imageio:imageio-jpeg:3.4.1'

    testImplementation group: 'junit', name: 'junit', version: '4.12'
}

/**
 * Task for getting all the dependencies downloaded.
 */
task getDeps(type: Copy) {
    from sourceSets.main.runtimeClasspath
    into 'dist/lib'
}

/**
 * Clean up the mess by the other tasks.
 */
clean.doFirst {
    delete 'dist'
}

/**
 * For now we do not use the tests given by the system.
 * We'll gradually add them back again.
 */
test {
    exclude 'net/semanticmetadata/lire/applications/**'
    exclude 'net/semanticmetadata/lire/benchmarking/**'
    exclude 'net/semanticmetadata/lire/builders/**'
    exclude 'net/semanticmetadata/lire/classifiers/**'
    exclude 'net/semanticmetadata/lire/indexers/**'
    exclude 'net/semanticmetadata/lire/searchers/**'
    exclude 'net/semanticmetadata/lire/imageanalysis/TestExtraction**'
    exclude 'net/semanticmetadata/lire/TestProperties**'
}
